/*
 * generated by Xtext 2.13.0
 */
package org.sbelang.dsl.validation

import java.util.HashMap
import java.util.List
import java.util.Map
import org.eclipse.emf.ecore.EObject
import org.eclipse.emf.ecore.EStructuralFeature
import org.eclipse.emf.ecore.util.EcoreUtil
import org.eclipse.xtext.nodemodel.util.NodeModelUtils
import org.eclipse.xtext.validation.Check
import org.sbelang.dsl.SbeLangDslValueUtils
import org.sbelang.dsl.sbeLangDsl.CompositeTypeDeclaration
import org.sbelang.dsl.sbeLangDsl.EnumDeclaration
import org.sbelang.dsl.sbeLangDsl.EnumValueDeclaration
import org.sbelang.dsl.sbeLangDsl.FieldDeclaration
import org.sbelang.dsl.sbeLangDsl.MemberPrimitiveTypeDeclaration
import org.sbelang.dsl.sbeLangDsl.MemberRefTypeDeclaration
import org.sbelang.dsl.sbeLangDsl.MemberTypeDeclaration
import org.sbelang.dsl.sbeLangDsl.MessageSchema
import org.sbelang.dsl.sbeLangDsl.PresenceConstantModifier
import org.sbelang.dsl.sbeLangDsl.PresenceModifiers
import org.sbelang.dsl.sbeLangDsl.SbeLangDslPackage
import org.sbelang.dsl.sbeLangDsl.SetChoiceDeclaration
import org.sbelang.dsl.sbeLangDsl.SetDeclaration
import org.sbelang.dsl.sbeLangDsl.SimpleTypeDeclaration
import org.sbelang.dsl.sbeLangDsl.TypeDeclaration
import org.sbelang.dsl.sbeLangDsl.VersionModifiers

import static org.sbelang.dsl.SbeLangDslValueUtils.isValidLiteral

/**
 * This class contains custom validation rules. 
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#validation
 */
class SbeLangDslValidator extends AbstractSbeLangDslValidator {

    public static val CHAR_PRIMITIVE = 'char'

    public static val NULL_VAL = 'NULL_VAL'

    @Check
    def checkAllTypeNamesAreUnique(MessageSchema messageSchema) {
        try {
            validateAllTypeNamesAreUnique(messageSchema.typeDelcarations.map[t|new NameDeclaration(t.name, t)],
                new HashMap<String, EObject>())
        } catch (Exception e) {
            e.printStackTrace
        }
    }

    @Check
    def checkField(FieldDeclaration fd) {
        val type = fd.fieldType
        val presence = fd.presenceModifiers
        validatePresence(presence, type)
    }

    @Check
    def checkMemberType(MemberPrimitiveTypeDeclaration mtd) {
        switch mtd.presence {
            PresenceConstantModifier:
                validatePresenceConstant(mtd.presence as PresenceConstantModifier, mtd.primitiveType)
        }
    }

    @Check
    def checkSet(SetDeclaration sd) {
        val maxValidBitIdx = switch (sd.encodingType) {
            case 'uint8': 7
            case 'uint16': 15
            case 'uint32': 31
            case 'uint64': 63
            default: -1
        }

        val Map<String, SetChoiceDeclaration> names = new HashMap()
        val Map<Integer, SetChoiceDeclaration> values = new HashMap()
        for (choice : sd.setChoices) {
            val existingName = names.put(choice.name, choice)
            if (existingName !== null) {
                val existingNode = NodeModelUtils.getNode(existingName)
                error(
                    '''Duplicate (case-insensitive) name [«choice.name»]; previous declaration at line «existingNode.startLine»''',
                    choice,
                    SbeLangDslPackage.Literals.SET_CHOICE_DECLARATION__NAME
                )
            }
            if ((choice.value < 0) || (choice.value > maxValidBitIdx))
                error(
                    '''Value is [«choice.value»] is ouside the valid range of [0,«maxValidBitIdx»] for «sd.encodingType»!''',
                    choice,
                    SbeLangDslPackage.Literals.SET_CHOICE_DECLARATION__VALUE
                )

            val existingValue = values.put(choice.value, choice)
            if (existingValue !== null) {
                val existingNode = NodeModelUtils.getNode(existingValue)
                error(
                    '''Value is [«choice.value»] collides with value of «existingValue.name» in line «existingNode.startLine»!''',
                    choice,
                    SbeLangDslPackage.Literals.SET_CHOICE_DECLARATION__VALUE
                )
            }

            if ((sd.versionModifiers !== null) && (choice.versionModifiers !== null)) {
                validateVersionAgainstContainer(choice.versionModifiers, sd.versionModifiers, choice,
                    SbeLangDslPackage.Literals.SET_CHOICE_DECLARATION__VERSION_MODIFIERS)
            }
        }
    }

    @Check
    def checkComposite(CompositeTypeDeclaration ctd) {
        val Map<String, MemberTypeDeclaration> names = new HashMap()
        for (cm : ctd.compositeMembers) {
            if (cm instanceof MemberPrimitiveTypeDeclaration) {
                val existingName = names.put(cm.name, cm)
                if (existingName !== null) {
                    val existingNode = NodeModelUtils.getNode(existingName)
                    error(
                        '''Duplicate (case-insensitive) name [«cm.name»]; previous declaration at line «existingNode.startLine»''',
                        cm,
                        SbeLangDslPackage.Literals.MEMBER_TYPE_DECLARATION__NAME
                    )
                }
            }
            if (cm instanceof MemberRefTypeDeclaration) {
                val existingName = names.put(cm.name, cm)
                if (existingName !== null) {
                    val existingNode = NodeModelUtils.getNode(existingName)
                    error(
                        '''Duplicate (case-insensitive) name [«cm.name»]; previous declaration at line «existingNode.startLine»''',
                        cm,
                        SbeLangDslPackage.Literals.MEMBER_TYPE_DECLARATION__NAME
                    )
                }
            }
        }
    }

    @Check
    def checkEnum(EnumDeclaration ed) {
        val Map<String, EnumValueDeclaration> names = new HashMap()
        val Map<String, EnumValueDeclaration> values = new HashMap()
        for (ev : ed.enumValues) {
            val existingName = names.put(ev.name, ev)
            if (existingName !== null) {
                val existingNode = NodeModelUtils.getNode(existingName)
                error(
                    '''Duplicate (case-insensitive) name [«ev.name»]; previous declaration at line «existingNode.startLine»''',
                    ev,
                    SbeLangDslPackage.Literals.ENUM_VALUE_DECLARATION__NAME
                )
            }

            val existingValue = values.put(ev.value, ev)
            if (existingValue !== null) {
                val existingNode = NodeModelUtils.getNode(existingValue)
                error(
                    '''Duplicate value [«ev.value»]; previous declaration at line «existingNode.startLine»''',
                    ev,
                    SbeLangDslPackage.Literals.ENUM_VALUE_DECLARATION__VALUE
                )
            }

            if (!isValidLiteral(ev.value, ed.encodingType)) {
                error(
                    '''Value is [«ev.value»] which is outside the valid range for encoding type [«ed.encodingType»]!''',
                    ev,
                    SbeLangDslPackage.Literals.ENUM_VALUE_DECLARATION__VALUE
                )
            }

            if (ev.value.toString == reservedNullValue(ed.encodingType).toString) {
                if (NULL_VAL != ev.name)
                    error(
                        '''Value [«ev.value»] is reserved for null in [«ed.encodingType»]!''',
                        ev,
                        SbeLangDslPackage.Literals.ENUM_VALUE_DECLARATION__VALUE
                    )
            }

            if ((ed.versionModifiers !== null) && (ev.versionModifiers !== null)) {
                validateVersionAgainstContainer(ev.versionModifiers, ed.versionModifiers, ev,
                    SbeLangDslPackage.Literals.ENUM_VALUE_DECLARATION__VERSION_MODIFIERS)
            }
        }
    }

    @Check
    def checkRangeIsProper(MemberPrimitiveTypeDeclaration mptd) {
        if(mptd.rangeModifiers === null) return; // no range can't be wrong
        if (mptd.presence !== null) {
            // if constant, range does not make sense...
            if (mptd.presence instanceof PresenceConstantModifier)
                error(
                    "You can't specify a range for a constant!",
                    SbeLangDslPackage.Literals.MEMBER_PRIMITIVE_TYPE_DECLARATION__RANGE_MODIFIERS
                )
        }

        if ((mptd.rangeModifiers.min !== null) && (mptd.rangeModifiers.max !== null)) {
            switch (mptd.primitiveType) {
                case 'char': {
                    val min = SbeLangDslValueUtils.parseCharacter(mptd.rangeModifiers.min)
                    val max = SbeLangDslValueUtils.parseCharacter(mptd.rangeModifiers.max)

                    if (min.isPresent && max.isPresent)
                        if (min.get.compareTo(max.get) > 0)
                            error(
                                '''Minimum range of («mptd.rangeModifiers.min») cannot exceed maximum of («mptd.rangeModifiers.max»)''',
                                SbeLangDslPackage.Literals.MEMBER_PRIMITIVE_TYPE_DECLARATION__RANGE_MODIFIERS
                            )
                }
                default: { // assume number...
                    val min = SbeLangDslValueUtils.parseBigDecimal(mptd.rangeModifiers.min)
                    val max = SbeLangDslValueUtils.parseBigDecimal(mptd.rangeModifiers.max)
                    if (min.isPresent && max.isPresent)
                        if (min.get.compareTo(max.get) > 0)
                            error(
                                '''Minimum range of («mptd.rangeModifiers.min») cannot exceed maximum of («mptd.rangeModifiers.max»)''',
                                SbeLangDslPackage.Literals.MEMBER_PRIMITIVE_TYPE_DECLARATION__RANGE_MODIFIERS
                            )
                }
            }
        }

        if (!isValidLiteral(mptd.rangeModifiers.min.toString, mptd.primitiveType)) {
            error(
                '''Minimum range of («mptd.rangeModifiers.min») is not within range of type («mptd.primitiveType»)''',
                SbeLangDslPackage.Literals.MEMBER_PRIMITIVE_TYPE_DECLARATION__RANGE_MODIFIERS
            )
        }

        if (!isValidLiteral(mptd.rangeModifiers.max.toString, mptd.primitiveType)) {
            error(
                '''Maximum range of («mptd.rangeModifiers.max») is not within range of type («mptd.primitiveType»)''',
                SbeLangDslPackage.Literals.MEMBER_PRIMITIVE_TYPE_DECLARATION__RANGE_MODIFIERS
            )
        }
    }

    @Check
    def checkVersionModifiersAgainstSchemaVersion(VersionModifiers vm) {
        val ms = EcoreUtil.getRootContainer(vm) as MessageSchema

        if (vm.sinceVersion !== null) {
            if (vm.sinceVersion > ms.schema.version) {
                error(
                    '''The sinceVersion(«vm.sinceVersion») value cannot be greater than the schema version(«ms.schema.version») value!''',
                    vm,
                    SbeLangDslPackage.Literals.VERSION_MODIFIERS__SINCE_VERSION
                )
            }

            if (vm.deprecatedSinceVersion !== null) {
                if (vm.deprecatedSinceVersion <= vm.sinceVersion) {
                    error(
                        '''The deprecatedSinceVersion(«vm.deprecatedSinceVersion») value must be greater than the since version(«vm.sinceVersion») value!''',
                        vm,
                        SbeLangDslPackage.Literals.VERSION_MODIFIERS__DEPRECATED_SINCE_VERSION
                    )
                }
            }
        }

        if (vm.deprecatedSinceVersion !== null) {
            if (vm.deprecatedSinceVersion > ms.schema.version) {
                error(
                    '''The deprecatedSinceVersion(«vm.deprecatedSinceVersion») value cannot be greater than the schema version(«ms.schema.version») value!''',
                    vm,
                    SbeLangDslPackage.Literals.VERSION_MODIFIERS__DEPRECATED_SINCE_VERSION
                )
            }
        }
    }

    // INTERNALS ------------------------------------------------------
    private def validatePresence(PresenceModifiers presenceModifiers, TypeDeclaration typeDeclaration) {
        switch (presenceModifiers) {
            PresenceConstantModifier:
                validatePresenceConstant(presenceModifiers, typeDeclaration)
        // TODO: PresenceOptionalModifier: any validation required for this?
        }
    }

    private def validatePresenceConstant(PresenceConstantModifier constantModifier, TypeDeclaration typeDeclaration) {
        switch typeDeclaration {
            SimpleTypeDeclaration:
                validatePresenceConstant(constantModifier, typeDeclaration.primitiveType)
//            EnumDeclaration:
//            SetDeclaration:
//            CompositeTypeDeclaration:
        }
    }

    private def validatePresenceConstant(PresenceConstantModifier constantModifier, String primitiveType) {
        if (! isValidLiteral(constantModifier.constantValue, primitiveType)) {
            error('''The value [«constantModifier.constantValue»] is not valid for the type [«primitiveType»]''',
                constantModifier, SbeLangDslPackage.Literals.PRESENCE_CONSTANT_MODIFIER__CONSTANT_VALUE)
        }
    }

    private static class NameDeclaration {
        String name;
        EObject declaringObject;

        new(String n, EObject o) {
            this.name = n
            this.declaringObject = o
        }
    }

    private def validateVersionAgainstContainer(VersionModifiers toValidate, VersionModifiers containerVersions,
        EObject errorTarget, EStructuralFeature errorTargetAttr) {
        if (toValidate.sinceVersion !== null) {
            if ((containerVersions.sinceVersion !== null) &&
                (toValidate.sinceVersion < containerVersions.sinceVersion)) {
                error(
                    '''Can not have a [sinceVersion=«toValidate.sinceVersion»] in the past relative to container's [sinceVersion=«containerVersions.sinceVersion»]''',
                    errorTarget,
                    errorTargetAttr
                )
            }
            if ((containerVersions.deprecatedSinceVersion !== null) &&
                (toValidate.sinceVersion >= containerVersions.deprecatedSinceVersion)) {
                error(
                    '''Can not have a [sinceVersion=«toValidate.sinceVersion»] after container is already deprecated [deprecatedSinceVersion=«containerVersions.deprecatedSinceVersion»]''',
                    errorTarget,
                    errorTargetAttr
                )
            }
        }

        if (toValidate.deprecatedSinceVersion !== null) {
            if ((containerVersions.sinceVersion !== null) &&
                (toValidate.deprecatedSinceVersion <= containerVersions.sinceVersion)) {
                error(
                    '''The [deprecatedSinceVersion=«toValidate.deprecatedSinceVersion»] must be after the container's [sinceVersion=«containerVersions.sinceVersion»]''',
                    errorTarget,
                    errorTargetAttr
                )
            }
            if ((containerVersions.deprecatedSinceVersion !== null) &&
                (toValidate.deprecatedSinceVersion > containerVersions.deprecatedSinceVersion)) {
                error(
                    '''Can not have a [deprecatedSinceVersion=«toValidate.deprecatedSinceVersion»] after container is already deprecated [deprecatedSinceVersion=«containerVersions.deprecatedSinceVersion»]''',
                    errorTarget,
                    errorTargetAttr
                )
            }
        }
    }

    private def void validateAllTypeNamesAreUnique(List<NameDeclaration> nameDeclarations, Map<String, EObject> names) {
        nameDeclarations.forEach [ nd |
            if (nd !== null) {
                val existing = names.put(nd.name, nd.declaringObject)
                if (existing !== null) {
                    val existingNode = NodeModelUtils.getNode(existing)
                    val featureId = switch nd.declaringObject {
                        MemberPrimitiveTypeDeclaration:
                            SbeLangDslPackage.Literals.MEMBER_TYPE_DECLARATION__NAME
                        MemberRefTypeDeclaration:
                            SbeLangDslPackage.Literals.MEMBER_TYPE_DECLARATION__NAME
                        default: // all others are descendants of type declaration
                            SbeLangDslPackage.Literals.TYPE_DECLARATION__NAME
                    }
                    error(
                        '''Duplicate (case-insensitive) name [«nd.name»]; previous declaration at line «existingNode.startLine»''',
                        nd.declaringObject,
                        featureId
                    )
                }
                if (nd.declaringObject instanceof CompositeTypeDeclaration) {
                    val composite = nd.declaringObject as CompositeTypeDeclaration
                    validateAllTypeNamesAreUnique(composite.compositeMembers.map [ cm |
                        switch cm {
                            // MemberPrimitiveTypeDeclaration and MemberRefTypeDeclaration are confined within composites
                            // so they are more like fields: they are NOT reusable types. Uniqueness check should be local
                            // ------------------------------------------------------------------------------------------------
                            // MemberPrimitiveTypeDeclaration:
                            // new NameDeclaration(cm.name, cm)
                            // MemberRefTypeDeclaration:
                            // new NameDeclaration(cm.name, cm)
                            SetDeclaration:
                                new NameDeclaration(cm.name, cm)
                            EnumDeclaration:
                                new NameDeclaration(cm.name, cm)
                            CompositeTypeDeclaration:
                                new NameDeclaration(cm.name, cm)
                        }
                    ], names)
                }

            }
        ]
    }

    def long reservedNullValue(String enumType) {
        switch enumType {
            case 'char': 0
            default: SbeLangDslValueUtils.maxValue(enumType).toBigInteger.longValue
        }
    }

}
