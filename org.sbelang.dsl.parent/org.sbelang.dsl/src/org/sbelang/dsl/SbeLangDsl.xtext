grammar org.sbelang.dsl.SbeLangDsl with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate sbeLangDsl "http://www.sbelang.org/dsl/SbeLangDsl"

MessageSchema:
    schema=SchemaDeclaration
    'types' '{'
    typeDelcarations+=TypeDeclaration+
    '}'
    messageDeclarations+=MessageDeclaration;

SchemaDeclaration:
// schema org.sbelang.example id=1 version=1
// schema org.sbelang.example id=1 version=1 bigEndian
// schema org.sbelang.example id=1 version=1 headerType=org.sbelang.common.Header
// schema org.sbelang.example id=1 version=1 headerType=org.sbelang.common.Header bigEndian

// Each message schema corresponds to a java package [name]
// The id maps a numeric identifier to the schema
// The version is a monotonically increasing number
    'schema' name=QName 'id' '=' id=INT 'version' '=' version=INT optionalAttrs=OptionalSchemaAttrs;

OptionalSchemaAttrs:
    {OptionalSchemaAttrs} ('headerType' '=' headerType=QName)? (bigEndian?='bigEndian'?);

TypeDeclaration:
    SimpleTypeDeclaration | EnumDeclaration | SetDeclaration | CompositeTypeDeclaration;

SimpleTypeDeclaration /* simpleDataType - primitiveTypeAttributes + versionAttributes */:
// length=INT characterEncoding=STRING for character types only
    'simpleType' name=Name ':' primitiveType=PrimitiveType ('[' length=OptionalInt ']')?
    versionModifiers=VersionModifiers;

EnumDeclaration:
    'enum' name=Name ':' encodingType=EnumEncodingType versionModifiers=VersionModifiers '{'
    enumValues+=EnumValueDeclaration+
    '}';

EnumValueDeclaration:
    name=Name '(' value=EnumValue ')' versionModifiers=VersionModifiers;

EnumValue:
    INT | CHAR;

VersionModifiers:
    {VersionModifiers}
    ('sinceVersion' '=' sinceVersion=OptionalInt)?
    ('deprecatedSinceVersion' '=' deprecatedSinceVersion=OptionalInt)?;

SetDeclaration:
    'set' name=Name ':' encodingType=SetEncodingType versionModifiers=VersionModifiers '{'
    setChoices+=SetChoiceDeclaration+
    '}';

SetChoiceDeclaration:
    name=Name '(' value=INT ')' versionModifiers=VersionModifiers;

CompositeTypeDeclaration:
    'composite' name=Name versionModifiers=VersionModifiers '{'
    compositeMembers+=CompositeMember+
    '}';

CompositeMember:
    MemberTypeDeclaration | CompositeTypeDeclaration;

MemberTypeDeclaration:
    EnumDeclaration |
    SetDeclaration |
    MemberRefTypeDeclaration /* XSD "refType" */ |
    MemberCharTypeDeclaration /* XSD "memberDataType for characters */ |
    MemberNumericTypeDeclaration /* XSD "memberDataType for integers" */;

MemberRefTypeDeclaration:
    name=Name ':' type=[SimpleTypeDeclaration]
    ('range' '=' rangeModifiers=CharRangeModifiers)?
    presence=CharPresenceModifiers?;

MemberCharTypeDeclaration:
    name=Name ':' primitiveType=CharPrimitive ('[' length=OptionalInt ']')?
    ('range' '=' rangeModifiers=CharRangeModifiers)?
    presence=CharPresenceModifiers?;

MemberNumericTypeDeclaration:
    name=Name ':' primitiveType=NumericPrimitive ('[' length=OptionalInt ']')?
    ('range' '=' rangeModifiers=NumericRangeModifiers)?
    presence=NumericPresenceModifiers?;

NumericRangeModifiers:
    {NumericRangeModifiers} '[' min=OptionalSignedInt? ',' max=OptionalSignedInt? ']';

NumericPresenceModifiers:
    NumericConstantModifiers | NumericOptionalModifiers;

NumericConstantModifiers:
    ('constant' '=' constantValue=SignedInt);

NumericOptionalModifiers:
    (optional?='optional') |
    ('nullValue' '=' nullValue=OptionalSignedInt);

CharRangeModifiers:
    {CharRangeModifiers} '[' (min=OptionalChar?) ',' (max=OptionalChar?) ']';

CharPresenceModifiers:
    CharConstantModifiers | CharOptionalModifiers;

CharConstantModifiers:
    ('constant' '=' (constantValue=OptionalChar));

CharOptionalModifiers:
    (optional?='optional') |
    ('nullValue' '=' (nullValue=OptionalChar));

MessageDeclaration:
    'message' block=BlockDeclaration;

GroupDeclaration:
    'group' block=BlockDeclaration;

BlockDeclaration:
    name=Name 'id' '=' id=INT versionModifiers=VersionModifiers '{'
    fieldDeclarations+=FieldDeclaration*
    groupDeclarations+=GroupDeclaration*
    '}';

FieldDeclaration:
    name=Name ':' fieldType=[TypeDeclaration] '@' id=INT 
    // TODO: add presence
    // TODO: add "valueRef" - Constant value of a field as a valid value of an enumeration; Valid only if presence= ”constant”  
    versionModifiers=VersionModifiers;

CharPrimitive:
    'char';

NumericPrimitive:
    'int8' | 'int16' | 'int32' | 'int64' | 'uint8' | 'uint16' | 'uint32' | 'uint64' | 'float' | 'double';

PrimitiveType:
    CharPrimitive | NumericPrimitive;

EnumEncodingType:
    'char' | 'uint8' | 'uint16';

SetEncodingType:
    'uint8' | 'uint16' | 'uint32' | 'uint64';

QName:
    ID ('.' ID)*;

Name:
    ID;

OptionalInt returns ecore::EIntegerObject:
    INT;

OptionalSignedInt returns ecore::EIntegerObject:
    SignedInt;

SignedInt returns ecore::EInt:
    '-'? INT;

OptionalChar returns ecore::ECharacterObject:
// A value converter extracts the proper character
    CHAR;

terminal CHAR:
    "'" ('\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | "'")) "'";